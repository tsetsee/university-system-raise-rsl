Course, Department

scheme Curricula =
class
  object
    C : Course,
    D : Department
  type
    Semester :: id_sem : Nat,
    Co_Course = C.ID_Subject -m-> Semester,
    Opt_in_Semester = Semester -m-> Courses_Sem,
    Courses_Sem = C.ID_Subject-set >< C.Courses -> Bool,
    Curriculum0 ::
      depart_c : D.ID_Department <-> chg_depart_c
      available_Cr : Bool <-> chg_available_Cr
      intrvl_c : C.Interval <-> chg_intrvl_c
      course_compulsory : Co_Course <-> chg_course_comp
      course_optional : Co_Course <-> chg_course_opt
      selection_c : Opt_in_Semester <-> chg_selectionc,
    Curriculum = {| cr : Curriculum0 :- is_wf_Curriculum(cr) |},
    ID_Curriculum,
    Currs = ID_Curriculum -m-> Curriculum
  value
    is_wf_Curriculum : Curriculum0 -> Bool
      is_wf_Curriculum(cr) is 
        let co = course_optional(cr), cc = course_compulsory(cr) in
          rng co <<= dom selection_c(cr) /\
          dom cc inter dom co = {} /\
          (available_Cr(cr) => cc union co ~= [])
        end,
    course_semester_in_curr : Curriculum >< C.Courses -> Bool
      course_semester_in_curr(cr, course) is
        let co = course_optional(cr) in
          (
            all ids : C.ID_Subject :-
              ids isin dom co => 
                let sem = co(ids) in
                  exists sids : C.ID_Subject :-
                    (all id : C.ID_Subject :- id isin sids => co(id) = sem) /\
                    ids isin sids /\
                    sids <<= dom co /\ selection_c(cr)(sem)(sids, course)
                end
          )
        end,
    courseset_in_dept : D.ID_Department >< C.Courses -> ID_Subject-set
    courseset_in_dept(idd, course) is 
      {ids |
        ids : ID_Subject :-
          ids isin dom course /\ idd = depart_s(course(ids))
      },
      is_wf_Curr_Course_Dept : Currs >< C.Courses >< D.Depts -> Bool
      is_wf_Curr_Course_Dept(curr, course, dept) is 
        (
          all cr : Curriculum :-
            cr isin rng curr =>
            let idd = depart_c(cr), co_cr = dom courses_in_curr(cr) in
              idd isin dom dept /\
              is_wf_Curr_Course(cr, course) /\
              (
                co_cr ~= {} =>
                co_cr inter courseset_in_dept(idd, course) ~= {}
              )
            end
        ),
      is_wf_Curr_Course : Curriculum >< Courses -> Bool
      is_wf_Curr_Course(cr, course) is
        (
          let ccm = courses_in_curr(cr), co_cr = dom ccm in
            co_cr <<= dom course /\
            (
              all ids : ID_Subject :-
                ids isin co_cr =>
                let sub = course(ids), prq = dom prereq(sub) in
                  (
                    exists sids : ID_Subject-set :-
                    sids <<= prq /\
                    selection_s(sub)(sids) /\
                    sids <<= co_cr /\
                    (
                      all ids1 : ID_Subject :-
                        ids1 isin sids =>
                        id_sem(ccm(ids1)) <= id_sem(ccm(ids))
                    )
                  )
                end /\
                course_semester_in_curr(cr, course)
            )
          end
        ),
      courses_in_curr : Curriculum -> Co_Course
      courses_in_curr(cr) is
        course_compulsory(cr) union course_optional(cr)
end
